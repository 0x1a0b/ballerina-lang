import org.apache.tools.ant.taskdefs.condition.Os
apply plugin: 'base'

task npmInstall(type: Exec) {
    inputs.file("package-lock.json")
    outputs.dir("node_modules")

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm', 'install'
    } else {
        commandLine 'npm', 'install'
    }
}

task npmBuild(type: Exec) {
    mustRunAfter('npmInstall')
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm', 'run', 'build'
    } else {
        commandLine 'npm', 'run', 'build'
    }
}

assemble {
    dependsOn npmInstall
    dependsOn npmBuild
}

check.finalizedBy('test')

task test(type: Exec) {
    mustRunAfter('npmBuild')
    onlyIf {
        !npmInstall.state.upToDate || !npmBuild.state.upToDate
    }
    doFirst {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine 'cmd', '/c', 'npm', 'test'
        } else {
            commandLine 'npm', 'test'
        }
    }
}