/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'base'
apply from: "$rootDir/gradle/javaProject.gradle"

description = 'Ballerina - Testerina Test'

def extractedDist = "$buildDir/extractedDistribution/jballerina-tools-${project.version}"

configurations {
    jballerinaTools
}

dependencies {
    jballerinaTools project(path: ':jballerina-tools', configuration: 'zip')
    testCompile 'org.testng:testng'
    testCompile 'org.slf4j:slf4j-jdk14'
    implementation 'info.picocli:picocli'

    implementation project(path: ':ballerina-test-utils')
}

task extractDistribution(type: Copy) {
    dependsOn ':jballerina-tools:build'
    from zipTree(configurations.jballerinaTools.asPath)
    into "$buildDir/extractedDistribution"

}

// Task to execute unit tests written using Ballerina lang
task unitTests {
    dependsOn extractDistribution
    doLast {
        exec {
            println "Executing tests for assertTrue..."
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                workingDir "src/test/ballerina"
                commandLine 'cmd', '/c', "${extractedDist}/bin/ballerina.bat", 'test', '--groups', 'p1', 'assertions'
            } else {
                workingDir "src/test/ballerina"
                commandLine "${extractedDist}/bin/ballerina", 'test', '--groups', 'p1', 'assertions'
            }
        }
        exec {
            println "Executing tests for assertions excluding assertTrue..."
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                workingDir "src/test/ballerina"
                commandLine 'cmd', '/c', "${extractedDist}/bin/ballerina.bat", 'test', '--disable-groups', 'p1', '--all'
            } else {
                workingDir "src/test/ballerina"
                commandLine "${extractedDist}/bin/ballerina", 'test', '--disable-groups', 'p1', '--all'
            }
        }
    }
}

// Test task written using TestNG to execute cases that cannot be covered in the 'unitTests' task
test {
    dependsOn unitTests
    systemProperty 'enableTesterinaTests', 'true'

    maxParallelForks = 1
    systemProperty 'basedir', "$buildDir"
    systemProperty 'libdir', "$buildDir"
    systemProperty 'server.zip', configurations.jballerinaTools.asPath
    systemProperty 'jballerina.server.zip', configurations.jballerinaTools.asPath
    systemProperty 'java.util.logging.config.file', "$buildDir/src/test/resources/logging.properties"
    systemProperty 'java.util.logging.manager', 'org.ballerinalang.logging.BLogManager'

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}
